#pragma kernel InitializeOffsets
#pragma kernel CalculateOffsets

// Calculate offsets for the given sorted keys. For example, given:
// SortedKeys = {2, 2, 2, 3, 6, 6, 9, 9, 9, 9}
// Output will be ('x' represents irrelevant entries):
// Offsets = {x, x, 0, 3, x, x, 4, x, x, 6} 
// Now, to look up where the '6' keys start for instance, Offsets[6] gives the answer (4)

// NOTE: offsets buffer must be initialized with values equal to (or greater than) its length to ensure that this works correctly
StructuredBuffer<uint> SortedKeys;
RWStructuredBuffer<uint> Offsets;
uint numInputs;

[numthreads(256, 1, 1)]
void InitializeOffsets(uint3 id : SV_DispatchThreadID)
{
	if (id.x >= numInputs) { return; }
	Offsets[id.x] = numInputs;
}

[numthreads(256, 1, 1)]
void CalculateOffsets(uint3 id : SV_DispatchThreadID)
{
	if (id.x >= numInputs) { return; }

	uint i = id.x;
	uint null = numInputs;

	uint key = SortedKeys[i];
	uint keyPrev = i == 0 ? null : SortedKeys[i - 1];

	if (key != keyPrev)
	{
		Offsets[key] = i;
	}
}